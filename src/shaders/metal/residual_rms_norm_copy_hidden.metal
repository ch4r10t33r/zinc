#include <metal_stdlib>
#include <simd/simd.h>

using namespace metal;

struct Params {
    uint n;
    float eps;
    float scale;
    uint residual_offset;
};

// Fused out-of-place residual-add + RMS norm:
//   hidden_dst = hidden_src + scale * residual
//   norm_out   = weights * normalize(hidden_dst)
//
// This is the prompt-prefill companion to residual_rms_norm.metal for cases
// where the residual source buffer is still needed by an in-flight producer.
#define N_SIMDGROUPS 8
#define SIMD_WIDTH 32
#define TG_SIZE (N_SIMDGROUPS * SIMD_WIDTH)
#define MAX_PER_THREAD 128

kernel void main0(
    constant Params& p [[buffer(0)]],
    device const float* hidden_src [[buffer(1)]],
    device const float* residual [[buffer(2)]],
    device float* hidden_dst [[buffer(3)]],
    device float* norm_out [[buffer(4)]],
    device const float* weights [[buffer(5)]],
    uint tid [[thread_position_in_threadgroup]],
    uint group_id [[threadgroup_position_in_grid]],
    uint sg_idx [[simdgroup_index_in_threadgroup]],
    uint lane [[thread_index_in_simdgroup]]
) {
    threadgroup float partial_sums[N_SIMDGROUPS];

    const uint base = group_id * p.n;
    const float scale = p.scale;

    float vals[MAX_PER_THREAD];
    float sum_sq = 0.0f;
    uint count = 0;
    for (uint i = tid; i < p.n; i += TG_SIZE) {
        const float h = fma(scale, residual[p.residual_offset + base + i], hidden_src[base + i]);
        hidden_dst[base + i] = h;
        vals[count++] = h;
        sum_sq += h * h;
    }

    const float sg_sum = simd_sum(sum_sq);
    if (lane == 0) partial_sums[sg_idx] = sg_sum;
    threadgroup_barrier(mem_flags::mem_threadgroup);

    const float v = (lane < N_SIMDGROUPS) ? partial_sums[lane] : 0.0f;
    const float total_sq = simd_sum(v);
    const float rms_inv = fast::rsqrt(fast::divide(total_sq, float(p.n)) + p.eps);

    count = 0;
    for (uint i = tid; i < p.n; i += TG_SIZE) {
        norm_out[base + i] = weights[i] * (vals[count++] * rms_inv);
    }
}
